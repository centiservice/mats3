// mats-test-jupiter

dependencies {
    // This module is an "extension" of Jupiter (JUnit5) - and we employ classes from it, so API-level dependency.
    api "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"

    // This module extends the generic "Unit" implementations in mats-test
    // Also the few tools in there should be transitive, so want it exported as transitive dep. Using API.
    api project(':mats-test')

    // The SFL4J API. Since you should provide the SLF4J implementation yourself, we use compileOnly.
    compileOnly "org.slf4j:slf4j-api:$slf4jVersion"

    // Need an implementation of the Mats API
    implementation project(":mats-impl-jms")
    // Need the MatsSerializerJson
    implementation project(":mats-serial-json")
    // The Rule_MatsEndpoint is annotated with @Inject to hook into Spring injection if relevant.
    implementation "javax.inject:javax.inject:$javaxInjectVersion"

    // H2 Database, for database tests - but we do not want to make this dependency transitive, so compileOnly.
    compileOnly "com.h2database:h2:$h2Version"

    // :: TEST

    testImplementation project(':mats-spring-test')
    testImplementation project(':mats-util')
    testImplementation project(':mats-test-broker')

    // To test MatsAnnotatedClass
    testImplementation project(':mats-spring')

    // ..Removed in Java 11
    testImplementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"

    // JMS API - to "handle" the ConnectionFactory - provided by 'mats-test' via 'implementation'.
    testCompileOnly "$jmsApiDependency"

    // Logging
    testCompileOnly "org.slf4j:slf4j-api:$slf4jVersion"
    testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"

    // Single Spring test inside
    testImplementation ("org.springframework:spring-test:$springVersion") {
        exclude group:'junit', module:'junit'
    }
    testImplementation "org.springframework:spring-context:$springVersion"
    testImplementation "org.springframework:spring-tx:$springVersion"

    // The Jupiter Runtime..?
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
    testImplementation "org.junit.platform:junit-platform-launcher:$jupiterPlatformVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"

    // Mockito
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    // H2 database for testing DataSource parameter providing in @MatsTest
    testRuntimeOnly "com.h2database:h2:$h2Version"

}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Mats^3 Jupiter (JUnit 5) testing tools'
                description = 'Mats^3 testing tools for Jupiter (JUnit 5).'
            }
        }
    }
}